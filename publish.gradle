ext.configurePublishing = { packageName, packageDesc, packageVersion ->
    tasks.register('sourcesJar', Jar) {
        it.dependsOn classes
        it.archiveClassifier.set('sources')
        it.from sourceSets.main.allSource
    }
    tasks.register('javadocJar', Jar) {
        it.dependsOn javadoc
        it.archiveClassifier.set('javadoc')
        it.from javadoc.destinationDir
    }
    publishing {
        repositories {
            maven {
                name = 'ProjectLocal'
                url = new File(rootProject.getProjectDir().getAbsolutePath() + '/mavenRepo/');
            }
        }
        publications {
            imgui(MavenPublication) {
                groupId = 'com.github.kusaanko'
                artifactId = packageName
                version = packageVersion

                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = packageName
                    description = packageDesc
                    url = 'https://github.com/kusaanko/imgui-java'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/license/mit/'
                        }
                    }
                    developers {
                        developer {
                            id = 'SpaiR'
                            name = 'Ilya Prymshyts'
                            email = 'despsolver@gmail.com'
                        }
                    }
                }
            }
        }
    }
}
